plugins {
    id 'java'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

apply from: 'dependencies.gradle'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// to run tests in TestRunner sourceSets have to be set
sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

test {
    useJUnitPlatform()
    systemProperty "karate.env", System.properties.getProperty("karate.env")
}


tasks.register('karate', JavaExec) {
    group = 'karate tasks'
    description = 'allows running karate tests'

    mainClass = 'com.intuit.karate.cli.Main'
    classpath = sourceSets.test.runtimeClasspath

    if (project.hasProperty('tags')) {
        args = ['src/test/java', '--tags', project.tags]
    } else {
        args = ['src/test/java']
    }

    // karate reports will be saved in gradle build
    args += [
        '-o', 'build/karate_reports_from_karate_task',
        'src/test/java'
    ]

    systemProperty "karate.env", System.properties.getProperty("karate.env")

    // this allows running tests in parallel mode
    systemProperty "karate.options", System.properties.getProperty("karate.options")

    // karate task will be executed even when tests fail
    // ignoreExitValue = true as JavaExecSpec
}
